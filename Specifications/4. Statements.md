# 4. Statements
Code inside module bodies consist of a set of statements. These mainly break down into three different kinds:

* Declaration: Declaration of a variable, wire, register or instantiation of a module.
* Nondeclarative: Assignments/hooks to pre-declared wires or modules.
* Blocks: Blocks with special functionality.

## 4.1 Declarations
### 4.1.1 Variables, Wires and Registers
Variables, wires and registers are declared with the `Var`, `Wire` and `Register` keywords specifically.

Optionally, a user may elect to specify a bus width. The bus width is provided as a range. Ranges are declared as **software expressions** with two dots (**..**) in between.

(Like Perl and Ruby, both ends are inclusive. However, in Phi, a **...** exclusive range is not supported.)

For examples, `Var[31..0]` declares a 32-bit variable.

They are then followed by an identifier. Identifiers are essentially identically adapted from the Swift Programming Language with the omission of the implicit dollar sign (`$`) identifiers.

***
Identifiers begin with an uppercase or lowercase letter A through Z, an underscore (_), a noncombining alphanumeric Unicode character in the Basic Multilingual Plane, or a character outside the Basic Multilingual Plane that isn’t in a Private Use Area. After the first character, digits and combining Unicode characters are also allowed.

To use a reserved word as an identifier, put a backtick (\`) before and after it. For example, class is not a valid identifier, but \`class\` is valid. The backticks aren’t considered part of the identifier; \`x\` and x have the same meaning.

- ***Swift.org on Identifiers***
***

(Beta implementations of the reference compiler may not support non-ASCII characters as a limitation of GNU Flex. This will be addressed later with a toolchain adjustment.)

Again, optionally, a user may elect to specify an **array** declaration. Like in C, an array is not a fundemantal part of the type and instead has to do with the variable itself. Array declarations, unlike bus declarations, use only a software expression: ranges are not allowed.

```phi
    Register[31..0] registerFile[16];
```
***Snippet 0: Declaring a register file. This register file has 16 32-bit registers.***

Like C, you may assign to variables immediately upon declaration (where this assignment will become its **driver** as detailed in Section 1), but unlike C you may not assign to arrays, and individual variables can be used. The use of for-loops is recommended for arrays, and out-of-line assignment is covered in the **Nondeclarative** section.

```phi
    Wire[31..0] zero = 32b0;
```
***Snippet 1: Driving a wire immediately upon declaration.***

You may also declare multiple wires in a single bus declaration, as showed here:

```phi
    Wire[31..0] zero = 32b0, completelyUnrelatedWire, anotherCompletelyUnrelatedWire = 32xABCDEF00;
```
***Snippet 2: Declaring multiple wires.***

It is important to recall assigning to a register simply sets its reset value and does not drive the register. Nevertheless, only one reset value can be assigned.

### 4.1.2 Module Instantiation
Modules are instantiated by writing the module name and the instance name: for example `AOI aoi` is enough to instantiate a module.

Optionally, a module can be **hooked** upon declaration as well.
```phi
    Counter counter(clock: clock, reset: reset, output: Output);
```
***Snippet 3: Hooking a module.***

When hooking a module, **all inputs have to be hooked**. It is not possible to simply leave an input empty. Outputs can be selectively ignored.

As stated in Section 3, modules can have parameters. To choose the values for parameters, the following syntax is used:

```phi
    ParameterizableCounter<width: (32)> counter(clock: clock, reset: reset, output: Output);
```
***Snippet 4: Choosing a parameter for a parameterized module.***

But what's new here is that you can declare arrays of instances as well. Like variables, wires and buses, you can use an **array declaration** optionally after the identifier, and also like them, you may not hook an entire array on the same time: you will need to use a for loop. Out-of-line hooking is covered in the **Nondeclarative** section.

```phi
    FullAdder adders[32];
```
***Snippet 5: Declaring an array of modules.***

## 4.2 Nondeclarative
### 4.2.1 Assignments
You may assign wires, variables and registers after they have been declared.

This is the only way to assign to wires, variables or registers that are part of an array.

```phi
    completelyUnrelatedWire = 32b0;

    registerFile[0] = 32b0;
```
***Snippet 6: Various examples on out-of-line assignment.***

An advantage of this as well is that you could concatenate left hand side expressions as well:

```phi
    [cout, s] = a + b;
```
***Snippet 7: LHS Concatenation.***

### 4.2.2 Hooking
You may hook instances of modules after they have been declared.

This is the only way to hook instances that are part of an array.

```phi
    aoi(a: a, b: b, c: c, d: d, f: f);

    for i in 0..31 {
        adders[i](a: a[i], b: b[i], cin: cin[i], s: s[i], cout: cout[i + 1]);
    }
```
***Snippet 8: Various examples on out-of-line hooking.***

## 4.3 Block-based
A lot of code in Phi is based around **blocks**. The simplest example is a  module's body is a block. Blocks are bounded in two braces ({}), C-style.

Blocks can either exist at the top level of the module or inside each other. There are various uses for blocks, from compile-time metadefinition to modeling combinational logic procedurally.

### 4.3.1 The `if` statement
Back by popular demand, the `if` statement is perhaps the most ubiquitous statement in programming.

The `if` statement can be used at the top level of a module, and it uses a **1-bit software expression** as its condition.

Unlike C, parentheses are not required for the condition expression but elision of braces for single statements is not possible. We have found that this practice is frankly worth eliminating altogether.

Users may chain an if statement with if and else if.

All elaboration for if statements happens at compile time. Therefore, `if` statements cannot use **hardware expressions** outside of an comb block and declarations are not allowed inside if statements.

```phi
    if a {
        x = 3b000;
    } else if b {
        x = 3b001;
    } else {
        x = 3b000;
    }
```
***Snippet 9: An example if statement.***

### 4.3.2 The `for` loop
Instead of the C-style for loop, which has generally shown its age, Phi uses the Perl-style for loop, with the syntax `for` identifier `in` range then the block.

```phi
    for i in 0..10 {
        // Code
    }
```
***Snippet 10: An example for loop.***

As seen in Snippet 8, the for loop can be used for out-of-line assignment and hooking. Like the if statement, declarations are not allowed inside the for loop.

### 4.3.3 The `comb` block
Phi supports what is known as **procedural modeling** for combinational logic. What this essentially is is that a series of statements can be used to treat components as procedural software, which is then executed in perpetuity. In Phi, the `comb` block is used for this.

To enable a procedural programming paradigm, the entire comb block counts as one driver. Here, the final value will drive the wire.

There are some key requirements for comb blocks:

    A. No declarations can occur inside comb blocks.

    B. If an object is assigned to inside an comb block, the assignment must occur under all conditions.

    C. A wire cannot be a function of itself, i.e. it cannot depend on its own value.


```phi
    comb {
        a = b;
        a = a + 1;
    }

    comb {
        if (en) {
            a = b;
        }
    }

    comb {
        a = a + 1;
    }
```
***Snippet 11: Example comb blocks.  The second is not allowed as assignment does not occur under all conditions. The third is not allowed as the value of a is a function of itself.***


`for` loops work identically, but there are some differences to the if statement and there is a a block-based statement that is only usable inside comb blocks.

#### 4.3.3.1 The `if` statement inside comb blocks
Inside an comb block, the if statament behaves differently in that the condition must be a 1-bit **bus** expression.

Additionally, `else` blocks are almost always a requirement, as per requirement B of comb blocks. You can also choose an initial assignment if it suits you.

```phi
    comb {
        if (a) {
            b = n;
        } else {
            b = s;
        }
    }
```
***Snippet 12: An example if condition inside a comb block.***

#### 4.3.3.2 The `switch` statement
Switch statements work much like they would in C, except:
* Declarations inside the switch body are not allowed (as per requirement A of comb blocks.)
* There is no fallthrough: execution stops when the next case is reached.
    * Unlike the Swift Programming Language which has a similar design for switch statements, there is no option to force a fallthrough.
* Switch statements must be exhaustive (as per requirement B of comb blocks.)

(You could think of it as analogous to the Verilog `parallel_case` if you're familiar with that.)

The switch statement: takes one hardware expression, but like the `mux` statement detailed in Section 1, you could use a special fixed width binary, octal or hexadecimal integer with `z` in the place of one of the digits to indicate a "don't care" value.

(Unlike in Verilog, the `z` digit can only be used in this context.)

```phi
    comb {
        switch operation {
            case 3b11x: out = shift; // Accepts both 3b111 and 3b110;
            case 3b101: out = slt;
            case 3b100: out = ~a;
            case 3b011: out = a ^ b;
            case 3b010: out = a | b;
            case 3b001: out = a & b;
            case 3b000: out = add;
        }
    }
```
***Snippet 12: An example switch statement.***

#### 4.3.3.3 Annotations
Having to write `else`s or exhaustive switches can become tedious, so two annotations can make this slightly easier:

`@elseZero` will make it so any unspecified elses makes the wire values default to zero.

`@elseRetain` will make it so any unspecified elses makes the wire keep its same value, **waivering requirement B** which may synthesize **latches**. This is only recommended for advanced users and may not make it to the stable version of Phi.

```phi
    @elseZero comb {}

    @elseRetain comb {}
```
***Snippet 13: Examples for annotations on comb blocks.***



